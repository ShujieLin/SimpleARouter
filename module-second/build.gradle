if (isRelease){
    apply plugin: 'com.android.library'
}else {
    apply plugin: 'com.android.application'
}

android {
    compileSdkVersion app_android.compileSdkVersion

    defaultConfig {
        if (!isRelease){
            applicationId app_appid.module_second
        }
        minSdkVersion app_android.minSdkVersion
        targetSdkVersion app_android.targetSdkVersion

        buildConfigField("boolean", "isRelease", String.valueOf(isRelease))

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName(), packageNameForAPT: packageNameForAPT]
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets{
        main{
            if (isRelease){
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //debug包的文件不合并到主工程
                java{
                    exclude('**/debug/**')
                }
            }else {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            }
        }
    }
}



dependencies {
//    implementation 'androidx.appcompat:appcompat:1.4.1'
//    implementation 'com.google.android.material:material:1.5.0'
//    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    app_dependencies.each { k, v -> implementation(v)}
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //公共基础库
    implementation project(":common")
    //arouter：
    //用于注解
    implementation project(":arouter-annotations")
    //注解处理器
    annotationProcessor project(":compiler")
}